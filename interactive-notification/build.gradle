buildscript {
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/chattylabs/maven" }
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.8.1'
    }
}

repositories {
    maven { url "https://dl.bintray.com/chattylabs/maven" }
}

apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'com.chattylabs.versioning'
apply from: '../buildsystem/android.gradle'

androidExtensions {
    experimental = true
}

versioning {

    tagPrefix "version/"

    keywords {
        minor "[feature]"
        patch "[bug]", "[patch]"
    }
}

android {
    defaultConfig {
        versionCode versioning.code()
        versionName versioning.name()
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Android
    compileOnly "com.android.support:appcompat-v7:$support_library"

    // Core
    compileOnly "com.chattylabs:android-commons:$android_commons"

    // Dagger
    compileOnly 'com.google.dagger:dagger:2.15'
    kapt 'com.google.dagger:dagger-compiler:2.15'
}

group = 'com.chattylabs.sdk.android'
version = android.defaultConfig.versionName

publish {
    userOrg = 'chattylabs'
    groupId = project.group
    artifactId = project.name
    publishVersion = project.version
    desc = 'Android Interactive Notification (VUI SDK)'
    website = 'https://github.com/chattylabs/interactive-notification'
}
